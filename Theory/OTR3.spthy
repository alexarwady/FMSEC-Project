theory OTR3
begin

builtins: signing, hashing, diffie-hellman

// pre-shared key distribution
rule Register_Ltk:
  [ Fr(~sk) ] --> [ !Ltk($A, ~sk), !Pk($A, pk(~sk)), Out(pk(~sk)) ]
  
rule Reveal_Ltk:
  [ !Ltk(A, sk) ] --[ Reveal(A) ]-> [ Out(sk) ]

/* We formalize the following protocol
  protocol {
    1. A -> B: g^x, {'I', g^x, B}skA, vkA
    1. A <- B: g^y, {'R', g^y, A}skB, vkB
  }
*/

rule A_Init:
  [ Fr(~id), 
  !Ltk(A, skA), 
  !Pk(B, pkB) ] 
  --[Create(~id, 'I')]-> 
  [ St_A_0(A, ~id, B, skA, pkB) ]

rule B_Init:
  [ Fr(~id), 
  !Ltk(B, skB), 
  !Pk(A, pkA) ] 
  --[Create(~id, 'R')]-> 
  [ St_B_0(B, ~id, A, skB, pkA) ]

rule A_Send:
  [ St_A_0(A, id, B, skA, pkB), Fr(~x) ] --[Running(A, B, <'R', 'I', 'g'^~x>)]-> [ St_A_1(A, id, B, skA, pkB, ~x) , Out(<'g'^~x, sign(<'I', 'g'^~x, B>, skA), pk(skA)>) ]

rule B_Rec:
  [ St_B_0(B, id, A, skB, pkA), In(<gx, sgx, pkA>) ] --[ Eq(verify(sgx, <'I', gx, B>, pkA), true) ]-> [ St_B_1(B, id, A, skB, pkA, gx) ]
  
rule B_Send:
  let Kab = h(gx^~y) in
  [ St_B_1(B, id, A, skB, pkA, gx), Fr(~y) ] --[ Secret(B, Kab), Honest(B) , Honest(A), Running(B, A, <'I', 'R', <Kab, gx, 'g'^~y>>), Commit(B, A, <'R', 'I', gx>) ]-> [ St_B_2(B, id, A, skB, pkA, gx, ~y, Kab) , Out(<'g'^~y, sign(<'R', 'g'^~y, A>, skB), pk(skB)>) ]
  
rule A_Rec:
  let Kab = h(gy^x) in
  [ St_A_1(A, id, B, skA, pkB, x), In(<gy, sgy, pkB>) ] --[ Eq(verify(sgy, <'R', gy, A>, pkB), true), Secret(A, Kab), Honest(A), Honest(B), Commit(A, B, <'I', 'R', <Kab, 'g'^x, gy>>) ]-> [ St_A_2(A, id, B, skA, pkB, x, gy, Kab) ]
  
restriction Equality:
  "All x y #i. Eq(x,y) @i ==> x = y"
  
restriction unique_i:
	"All id1 id2 #k1 #k2. Create(id1, 'I') @k1 & Create(id2, 'I') @k2 ==> id1 = id2"
	
restriction unique_r:
	"All id1 id2 #k1 #k2. Create(id1, 'R') @k1 & Create(id2, 'R') @k2 ==> id1 = id2"

// This lemma gets verified
lemma executability:
  exists-trace
    "Ex A B Kab #i #j.
      Secret(A, Kab)@i
      & Secret(B, Kab)@j
      & not (A = B)
      & not (Ex X #k. Reveal(X)@k)"

// This lemma gets verified      
lemma noninjectiveagreementINITIATOR:
  "All a b t #i. 
    Commit(a,b,<'I','R',t>) @i
    ==> (Ex #j. Running(b,a,<'I','R',t>) @j)
        | (Ex X #r. Reveal(X)@r & Honest(X) @i)"

// This lemma gets verified
lemma injectiveagreementINITIATOR:
  "All a b t #i. 
    Commit(a,b,<'I','R',t>) @i
    ==> (Ex #j. Running(b,a,<'I','R',t>) @j 
        & not (Ex a2 b2 #i2. Commit(a2,b2,<'I','R',t>) @i2
                           & not (#i2 = #i)))
              | (Ex X #r. Reveal(X)@r & Honest(X) @i)"

// This lemma gets verified
lemma noninjectiveagreementRESPONDER:
  "All a b t #i. 
    Commit(a,b,<'R','I',t>) @i
    ==> (Ex #j. Running(b,a,<'R','I',t>) @j)
        | (Ex X #r. Reveal(X)@r & Honest(X) @i)"

// This lemma gets verified
lemma injectiveagreementRESPONDER:
  "All a b t #i. 
    Commit(a,b,<'R','I',t>) @i
    ==> (Ex #j. Running(b,a,<'R','I',t>) @j 
              & not (Ex a2 b2 #i2. Commit(a2,b2,<'R','I',t>) @i2
                                 & not (#i2 = #i)))
              | (Ex X #r. Reveal(X)@r & Honest(X) @i)"
            
end
