theory P2b
begin

builtins: symmetric-encryption

functions: mac/2

equations: mac(m, k) = senc(m, k)

// pre-shared key distribution
rule Register_psk:
  [ Fr(~k) ]
  -->
  [ !Psk($A, $B, ~k)] 

rule Reveal_pskA:
  [ !Psk(A, B, k) ] --[ Reveal(A) ]-> [ Out(k) ]

rule Reveal_pskB:
  [ !Psk(A, B, k) ] --[ Reveal(B) ]-> [ Out(k) ]

/* We formalize the following protocol
  protocol {
    1. A -> B: x
    1. A <- B: y
    2. A -> B: ['I', 'R', x, y]k(B,A)
    2. A <- B: ['R', 'I', x, y]k(B,A)
  }
*/

rule A_Init:
  [ Fr(~id), 
  !Psk(A, B, kAB),
  !Psk(B, A, kBA)] 
  --[Create(~id, 'I'), RegisterKeys(A, B, kAB, kBA)]-> 
  [ St_A_0(A, ~id, B, kAB, kBA) ]

rule B_Init:
  [ Fr(~id), 
  !Psk(A, B, kAB),
  !Psk(B, A, kBA)] 
  --[Create(~id, 'R'), RegisterKeys(B, A, kBA, kAB)]-> 
  [ St_B_0(B, ~id, A, kBA, kAB) ]

rule A_Send_x:
  [ Fr(~x), St_A_0(A, id, B, kAB, kBA) ] --> [ St_A_1(A, id, B, kAB, kBA, ~x), Out(~x) ]

rule B_Rec_x:
  [ St_B_0(B, id, A, kBA, kAB), In(x) ] --> [ St_B_1(B, id, A, kBA, kAB, x) ]
  
rule B_Send_y:
  [ Fr(~y), St_B_1(B, id, A, kBA, kAB, x) ] 
  --[ Running(B, A, <'I', 'R', <x, ~y>>) ]-> 
  [ St_B_2(B, id, A, kBA, kAB, x, ~y), Out(~y) ]
  
rule A_Rec_y:
  [ St_A_1(A, id, B, kAB, kBA, x), In(y) ] --> [ St_A_2(A, id, B, kAB, kBA, x, y) ]
  
rule A_Send_mac_y:
  [ St_A_2(A, id, B, kAB, kBA, x, y) ] 
  --[ Running(A, B, <'R', 'I', <x, y>>) ]-> 
  [ St_A_3(A, id, B, kAB, kBA, x, y), Out(mac(<'I', 'R', x, y>, kAB)) ]
  
rule B_Rec_mac_y:
  [ St_B_2(B, id, A, kBA, kAB, x, y), In(mac(<'I', 'R', x, y>, kAB)) ]
  --[ Commit(B, A, <'R', 'I', <x, y>>) , Honest(B) , Honest(A) ]->
  [ St_B_3(B, id, A, kBA, kAB, x, y) ]

rule B_Send_mac_x:
  [ St_B_3(B, id, A, kBA, kAB, x, y) ] 
  --[ Finish(B, A, 'R', kBA, kAB, x, y)]->
  [ St_B_4(B, id, A, kBA, kAB, x, y) , Out(mac(<'R', 'I', x, y>, kBA)) ]

rule A_Rec_mac_x:
  [ St_A_3(A, id, B, kAB, kBA, x, y), In(mac(<'R', 'I', x, y>, kBA)) ]
  --[ Commit(A, B, <'I', 'R', <x, y>>), Honest(A), Honest(B), Finish(A, B, 'I', kAB, kBA, x, y) ]->
  [ St_A_4(A, id, B, kAB, kBA, x, y) ]

restriction Unidirectional_Keys:
  "All a b kab kba #i.
    RegisterKeys(a, b, kab, kba) @i
    & kab = kba
      ==> F"

// This lemma gets verified
lemma executable:
  exists-trace
    "Ex a b x y kab kba #i #j.
      Finish(a, b, 'I', kab, kba, x, y) @i
      & Finish(b, a, 'R', kba, kab, x, y) @j
      & (All id1 id2 #k1 #k2. Create(id1, 'I') @k1 & Create(id2, 'I') @k2 ==> id1 = id2)
      & (All id1 id2 #k1 #k2. Create(id1, 'R') @k1 & Create(id2, 'R') @k2 ==> id1 = id2)
      & not(a = b)
      & not(Ex X #k. Reveal(X)@k)"

// This lemma gets verified
lemma injectiveagreementINITIATOR:
  "All a b t #i. 
    Commit(a,b,<'I','R',t>) @i
    ==> (Ex #j. Running(b,a,<'I','R',t>) @j 
        & not (Ex a2 b2 #i2. Commit(a2,b2,<'I','R',t>) @i2
                           & not (#i2 = #i)))
              | (Ex X #r. Reveal(X)@r & Honest(X) @i)"

// This lemma gets verified
lemma injectiveagreementRESPONDER:
  "All a b t #i. 
    Commit(a,b,<'R','I',t>) @i
    ==> (Ex #j. Running(b,a,<'R','I',t>) @j 
              & not (Ex a2 b2 #i2. Commit(a2,b2,<'R','I',t>) @i2
                                 & not (#i2 = #i)))
              | (Ex X #r. Reveal(X)@r & Honest(X) @i)"

end
