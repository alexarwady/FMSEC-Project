theory P1
begin

builtins: symmetric-encryption

functions: mac/2

equations: mac(m, k) = senc(m, k)

// pre-shared key distribution
rule Register_psk:
  [ Fr(~k) ]
  -->
  [ !Psk($A, $B, ~k)] 

rule Reveal_pskA:
  [ !Psk(A, B, k) ] --[ Reveal(A) ]-> [ Out(k) ]

rule Reveal_pskB:
  [ !Psk(A, B, k) ] --[ Reveal(B) ]-> [ Out(k) ]

/* We formalize the following protocol
  protocol {
    1. A -> B: x
    1. A <- B: [x]k(B,A)
  }
*/

rule A_Init:
  [ Fr(~id), 
  !Psk(A, B, kAB),
  !Psk(B, A, kBA)] 
  --[Create(~id, 'I'), RegisterKeys(A, B, kAB, kBA)]-> 
  [ St_A_0(A, ~id, B, kAB, kBA) ]

rule B_Init:
  [ Fr(~id), 
  !Psk(A, B, kAB),
  !Psk(B, A, kBA)] 
  --[Create(~id, 'R'), RegisterKeys(B, A, kBA, kAB)]-> 
  [ St_B_0(B, ~id, A, kBA, kAB) ]

rule A_Send_x:
  [ Fr(~x), St_A_0(A, id, B, kAB, kBA) ] --> [ St_A_1(A, id, B, kAB, kBA, ~x), Out(~x) ]

rule B_Rec_x:
  [ St_B_0(B, id, A, kBA, kAB), In(x) ] --> [ St_B_1(B, id, A, kBA, kAB, x) ]

rule B_Send_mac_x:
  [ St_B_1(B, id, A, kBA, kAB, x) ] 
  --[ Running(B, A, <'I', 'R', x>) ]-> 
  [ St_B_2(B, id, A, kBA, kAB, x), Out(mac(x, kBA)) ]

rule A_Rec_mac_x:
  [ St_A_1(A, id, B, kAB, kBA, x), In(mac(x, kBA)) ]
  --[ Commit(A, B, <'I', 'R', x>), Honest(A), Honest(B), Finish() ]->
  [ St_A_2(A, id, B, kAB, kBA, x) ]

restriction Unidirectional_Keys:
  "All a b kab kba #i.
    RegisterKeys(a, b, kab, kba) @i
    & kab = kba
      ==> F"

// This lemma gets verified
lemma executable:
  exists-trace
    "Ex #i. Finish() @i & not (Ex X #j. Reveal(X)@j)"

// This lemma gets verified
lemma injectiveagreementINITIATOR:
  "All a b t #i.
    Commit(a,b,<'I','R',t>) @i
    ==> (Ex #j. Running(b,a,<'I','R',t>) @j
        & not (Ex a2 b2 #i2. Commit(a2,b2,<'I','R',t>) @i2
                           & not (#i2 = #i)))
              | (Ex X #r. Reveal(X)@r & Honest(X) @i)"

end
